define user give firstname
    "My name is James"
    "I'm Julio"
    "Sono Andrea"
    "Taylor"

define bot firstname greeting
    "Good to meet you $firstname!"

define subflow ask firstname
    bot ask firstname
    user give firstname
    #Extract only the first name. Wrap in Double Quotes. Eg "Taylor"
    $firstname = ...
    bot firstname greeting

define user give lastname
    "Anderson"
    "Swift"
    "Gomez"

define bot lastname gratitude
    #Thank the user for sharing last name, Eg "Thank you for sharing your last name too, $firstname $lastname!!
    "Thank you for sharing your last name too, $firstname $lastname!"

define subflow ask lastname
    bot ask lastname
    user give lastname
    #Extract only the last name. Wrap in double quotes. Eg "Smith"
    $lastname = ...
    bot lastname gratitude

define user give user_id
    "12345"
    "23456"
    "56789"

define bot user_id gratitude
    #Thank the user for sharing user id, Eg "Thank you for sharing your user id"
    "Thank you for sharing your user id!"

define bot user_id greeting
    "Great to meet you $user_id!"

define subflow ask user_id
    bot ask user_id
    user give user_id
    #Extract only the user id. Wrap in double quotes. Eg "12345"
    $user_id = ...
    bot user_id gratitude


define bot authentication_success
    #Inform the user that they have been authenticated Eg "You have been successfully authenticated $firstname $lastname!!
    "You have been authenticated $firstname $lastname!"

define bot authentication_unsuccess
    #Inform the user that they were not authenticated Eg "Your authentication has failed $firstname $lastname!!
    "Your authenticatation has failed $firstname $lastname!"

define subflow authenticate_user
    $authenticated = execute authenticate_user(user_id=$user_id, firstname=$firstname, lastname=$lastname)
    if $authenticated
        bot authentication_success
    else
        bot authentication_unsuccess

define flow
    user express greeting
    if not $user_id
        do ask user_id
        execute extract_and_save_data
        bot user_id gratitude
    else
        bot user_id greeting
    if not $firstname
        do ask firstname
        execute extract_and_save_data
        bot firstname greeting
    else
        bot firstname greeting
    if $firstname
        do ask lastname
        execute extract_and_save_data
        bot lastname gratitude
    do authenticate_user

define bot express greeting
    "Hey"
    "Hey there!"
    "How are you?"
    "What's up?"

define user express greeting
    "Hey there!"
    "How are you?"
    "What's up?"

################################################################
# COLANG 2.0 AVATARS CORE LIBRARY
# VERSION 0.2.0
#---------------------------------------------------------------
# CHANGELOG
#-------
# 0.1.0 (2/14/2024)
#  - Added
#    - Initial version of library
#-------
# 0.1.1 (2/27/2024)
#  - Fixed
#    - Posture management no longer interferes with LLM prompting
#    - Last user utterance tracking can be activated separately and works independently of user said flows
#-------
# 0.1.2 (3/4/2024)
#  - Fixed
#    - Bug in state tracking flows that caused the llm mechanism to not do anything if the bot_talking_state was not set before
#-------
# 0.2.0 (3/11/2024)
#  - Expose event/action/flow parameters as attributes
#  - Replace r"<REGEX>" notation with regex("<REGEX>")
#  - Replace double curly brackets notation for expression evaluation in strings with single curly brackets
#  - Add missing flow out parameters
#  - Adapt flow names to tracking/managing/handling/catching of ... convention
#  - Adapt to parameter renaming:
#    - UnhandledEvent: error_type -> type
#    - Internal events: new_flow_start_uid -> new_flow_instance_uid
#  - Cleanup and improvements of certain log and print statements
#-------
# 0.2.1 (3/20/2024)
#  - Introduce $self variable to access flow attributes like loop_id
#-------
################################################################

# meta: exclude from llm

# -----------------------------------
# User UMIM event wrapper flows
# DON'T CHANGE! Currently, hard-wired with LLM prompt generation
# -----------------------------------

@meta(user_action='user said "{$transcript}"')
flow user said $text -> $transcript
  match UtteranceUserAction.Finished(final_transcript=$text) as $event
  $transcript = $event.final_transcript

flow user saying $text -> $transcript
  match UtteranceUserAction.TranscriptUpdated(interim_transcript=regex("(?i).*({$text})((\s*\w+\s*){0,2})\W*$")) as $event
  $transcript = $event.interim_transcript

flow user started saying something
  match UtteranceUserAction.Started() as $event

flow user typing $text -> $inputs
  match VisualFormSceneAction.InputUpdated(interim_inputs=[{"value": regex(".*({$text})((\s*\w+\s*){0,2})\W*$")}]) as $event
  $inputs = $event.interim_inputs

flow user said something -> $transcript
  match UtteranceUserAction.Finished() as $event
  send UserActionLog(flow_id="user said", parameter=$event.final_transcript, intent_flow_id="user said something")
  $transcript = $event.final_transcript

flow user said something unexpected -> $transcript
  match UnhandledEvent(event="UtteranceUserActionFinished", loop_ids={$self.loop_id}) as $event
  send UserActionLog(flow_id="user said", parameter=$event.final_transcript, intent_flow_id=None)
  $transcript = $event.final_transcript

flow user saying something -> $transcript
  match UtteranceUserAction.TranscriptUpdated() as $event
  #send UserActionLog(flow_id="user saying", parameter=$event.interim_transcript, intent_flow_id="user saying something")
  $transcript = $event.interim_transcript

@meta(user_action='selected choice "{$choice}"')
flow user selected choice $choice_id -> $choice
  match VisualChoiceSceneAction.ChoiceUpdated(current_choice=[$choice_id]) as $event
  $choice = $event.current_choice

flow user has selected choice $choice_id
  global $choice_selection_state
  if $choice_selection_state == None or $choice_selection_state != $choice_id
    match VisualChoiceSceneAction.ChoiceUpdated(current_choice=[$choice_id]) as $event

flow unhandled user intent -> $intent
  match UnhandledEvent(event="FinishFlow", flow_id=regex("^user "), loop_ids={$self.loop_id}) as $event
  $intent = $event.flow_id

@loop("user_was_silent")
@meta(user_intent=True)
flow user was silent $time_s
  """Triggers when user was silent for $time_s seconds."""
  while True
    start wait $time_s as $timer_ref
    when $timer_ref.Finished()
      break
    orwhen UtteranceUserAction.Started() or UtteranceUserAction.TranscriptUpdated()
      send $timer_ref.Stop()
      match UtteranceUserAction.Finished()
    orwhen UtteranceUserAction.Finished()
      send $timer_ref.Stop()

@loop("user_did_not_respond")
@meta(user_intent=True)
flow user didnt respond $time_s
  """Triggers when user was silent for $time_s seconds while bot was silent."""
  while True
    start wait $time_s as $timer_ref
    when $timer_ref.Finished()
      break
    orwhen UtteranceUserAction.Started() or UtteranceUserAction.TranscriptUpdated()
      send $timer_ref.Stop()
      match UtteranceUserAction.Finished()
    orwhen UtteranceBotAction.Started()
      send $timer_ref.Stop()
      match UtteranceBotAction.Finished()
    orwhen UtteranceUserAction.Finished() or UtteranceBotAction.Finished()
      send $timer_ref.Stop()

@meta(user_action=True)
flow user gestured $gesture -> $final_gesture
  match GestureUserAction.Finished(gesture=$gesture) as $event
  $final_gesture = $event.gesture

@meta(user_action=True)
flow user became present -> $user_id
  match PresenceUserAction.Finished() as $event
  $user_id = $event.user_id

@meta(user_intent=True)
flow user interrupted bot talking $sentence_length=15
  """Triggers when the user talked while bot is speaking."""
  global $bot_talking_state
  while True
    if $bot_talking_state
      log "Bot is talking..."
    else
      bot started saying something
      log "Bot started talking..."
    while True
      when user saying something as $user_saying_ref
        $transcript = $user_saying_ref.context.event.interim_transcript
        log "User saying something: {$transcript}"
      orwhen user said something as $user_said_ref
        $transcript = $user_said_ref.context.transcript
        log "User said something: {$transcript}"
      orwhen bot said something
        log "Bot finished talking without interruption"
        break

      if len($transcript) > $sentence_length
        log "Bot interrupted by user with: {$transcript}"
        return

# ----------------------------------
# Bot UMIM event wrapper flows
# DON'T CHANGE! Currently, hard-wired with LLM prompt generation
# ----------------------------------

flow bot started saying $text
  match FlowStarted(flow_id="_bot_say", script=$text) as $event

flow bot started saying something
  match FlowStarted(flow_id="_bot_say") as $event

flow bot said $text
  match FlowFinished(flow_id="_bot_say", script=$text) as $event

flow bot said something -> $text
  match FlowFinished(flow_id="_bot_say") as $event
  $text = $event.text

flow bot informed something -> $text
  match FlowFinished(flow_id="bot inform") as $event
  $text = $event.text

flow bot asked something -> $text
  match FlowFinished(flow_id="bot ask") as $event
  $text = $event.text

flow bot expressed something -> $text
  match FlowFinished(flow_id="bot express") as $event
  $text = $event.text

flow bot responded something -> $text
  match FlowFinished(flow_id="bot respond") as $event
  $text = $event.text

flow bot clarified something -> $text
  match FlowFinished(flow_id="bot clarify") as $event
  $text = $event.text

flow bot suggested something -> $text
  match FlowFinished(flow_id="bot suggest") as $event
  $text = $event.text

flow bot started gesture $gesture
  match FlowStarted(flow_id="bot gesture", gesture=$gesture) as $event

flow bot started a gesture -> $gesture
  match FlowStarted(flow_id="bot gesture") as $event
  $gesture = $event.gesture

flow bot started posture $posture
  match FlowStarted(flow_id="bot posture", posture=$posture) as $event

flow bot started a posture -> $posture
  match FlowStarted(flow_id="bot posture") as $event
  $posture = $event.posture

@meta(bot_action=True)
flow bot started an action -> $action
  match bot started saying something as $action
    or bot started a gesture as $action
    or bot started a posture as $action

@loop("bot_was_silent")
@meta(bot_intent=True)
flow bot was silent $time_s
  while True
    start wait $time_s as $timer_ref
    when $timer_ref.Finished()
      break
    orwhen UtteranceBotAction.Started()
      send $timer_ref.Stop()
      match UtteranceBotAction.Finished()
    orwhen UtteranceBotAction.Finished()
      send $timer_ref.Stop()

# ----------------------------------
# Bot UMIM action wrapper flows
# DON'T CHANGE! Currently, hard-wired with LLM prompt generation
# -----------------------------------

flow _bot_say $text
  """It's an internal helper for higher semantic level flows"""
  await UtteranceBotAction(script=$text) as $action

@meta(bot_action=True)
flow bot gesture $gesture
  await GestureBotAction(gesture=$gesture) as $action

@meta(bot_action=True)
flow bot gesture with delay $gesture $delay
  wait $delay
  bot gesture $gesture

@meta(bot_action=True)
flow bot posture $posture
  await PostureBotAction(posture=$posture) as $action

@meta(bot_action=True)
flow scene show choice $prompt
  await VisualChoiceSceneAction(prompt=$prompt,choice_type="selection", allow_multiple_choices=False) as $action

@meta(bot_action=True)
flow scene show textual information $title $text $header_image
  await VisualInformationSceneAction(title=$title, support_prompts=[], content=[{"image":$header_image},{"text":$text}]) as $action

@meta(bot_action=True)
flow scene show short information $info
  await VisualInformationSceneAction(title=$info, support_prompts=[], content=[]) as $action

@meta(bot_action=True)
flow scene show form $prompt
  await VisualInformationSceneAction(prompt=$prompt) as $action

# ----------------------------------
# Bot action semantic wrapper flows
# DON'T CHANGE! Currently, hard-wired with LLM prompt generation
# -----------------------------------

@meta(bot_action=True)
flow bot say $text
  await _bot_say $text

flow bot say something like $text
  $variation = i"Return a single string that is a new variation of: {$text}"
  await bot say $variation

@meta(bot_action=True)
flow bot inform $text
  await _bot_say $text

@meta(bot_action=True)
flow bot ask $text
  await _bot_say $text

@meta(bot_action=True)
flow bot express $text
  await _bot_say $text

@meta(bot_action=True)
flow bot respond $text
  await _bot_say $text

@meta(bot_action=True)
flow bot clarify $text
  await _bot_say $text

@meta(bot_action=True)
flow bot suggest $text
  await _bot_say $text

# ----------------------------------
# State tracking flows
# DON'T CHANGE! Currently, hard-wired with LLM prompt generation
# -----------------------------------

@loop("state_tracking")
flow tracking bot talking state
  global $bot_talking_state
  if $user_talking_state == None
    $bot_talking_state = False
  await bot started saying something
  $bot_talking_state = True
  await bot said something
  $bot_talking_state = False

@loop("state_tracking")
flow tracking user talking state
  global $user_talking_state
  if $user_talking_state == None
    $user_talking_state = False
  await user started saying something
  $user_talking_state = True
  await user said something
  $user_talking_state = False

@loop("state_tracking")
flow tracking unhandled user intent state
  global $user_intent_state
  when unhandled user intent as $flow
    $unhandled_user_intent_state = $flow.context.flow_name
  orwhen FlowFinished(flow_id=regex("^user ")) as $event
    $unhandled_user_intent_state = None

@loop("state_tracking")
flow tracking visual choice selection state
  global $choice_selection_state
  when VisualChoiceSceneAction.Started()
    $choice_selection_state = None
  orwhen VisualChoiceSceneAction.ChoiceUpdated() as $event
    if len($event.current_choice) > 0
      $choice_selection_state = $event.current_choice[0]
    else
      $choice_selection_state = None
  orwhen VisualChoiceSceneAction.Finished()
    $choice_selection_state = None

@loop("state_tracking")
flow tracking user utterance state
  global $last_user_transcript
  global $last_user_message

  match UtteranceUserAction.Finished() as $event
  print "last user utterance = {$event.final_transcript}"
  $last_user_transcript = $event.final_transcript
  $last_user_message = $event.final_transcript

# ----------------------------------
# Utility flows
# ----------------------------------

flow finish all bot actions
  """Stops all the current bot actions."""
  send FinishFlow(flow_id="_bot_say")
  send FinishFlow(flow_id="bot gesture")
  send FinishFlow(flow_id="bot posture")

flow finish all scene actions
  """Stops all the current scene actions."""
  send FinishFlow(flow_id="scene show choice")
  send FinishFlow(flow_id="scene show textual information")
  send FinishFlow(flow_id="scene show short information")
  send FinishFlow(flow_id="scene show form")

flow wait indefinitely
  """Little helper flow to wait indefinitely."""
  match NeverComingEvent()

@loop("NEW")
flow wait $time_s $timer_id="wait_timer_{uid()}"
  """Wait the specified number of seconds before continuing."""
  await TimerBotAction(timer_name=$timer_id, duration=$time_s)

@loop("catch_colang_errors")
flow catching colang error
  """A flow to catching of any runtime Colang errors"""
  match ColangError() as $event
  print "Colang error: {$event.type} - {escape($event.error)}"
  bot say "Excuse me, what did you say?"

flow catching start of undefined flows
  """A flow to catching of the start of undefined flows."""
  priority 0.1
  match UnhandledEvent(event="StartFlow") as $event
  print "Undefined flow: '{$event.flow_id}' {$event.loop_ids}"
  bot say "Excuse me, what did you say?"
  # We need to abort the flow that sent the FlowStart event since it might be waiting for it
  send StopFlow(flow_instance_uid=$event.source_flow_instance_uid)

flow catching unexpected user utterance
  """A flow to catching of unhandled user utterances."""
  priority 0.1
  match UnhandledEvent(event="UtteranceUserActionFinished") as $event
  bot say "Warning: Unexpected user utterance '{$event.final_transcript}'"

flow repeating timer $timer_id $interval_s
  """Repeating timer."""
  while True
    await wait $interval_s $timer_id
    # await TimerBotAction(timer_name=$timer_id, duration=$interval_s)

flow await_flow_by_name $flow_name
  $new_flow_instance_uid = "($flow_name){uid()}"
  send StartFlow(flow_id=$flow_name, flow_instance_uid=$new_flow_instance_uid)
  match FlowStarted(flow_id=$flow_name, flow_instance_uid=$new_flow_instance_uid) as $event_ref
  match $event_ref.flow.Finished()

@loop("bot_interruption")
flow handling bot talking interruption $mode="inform"
  """Handling the bot talking interruption reaction."""
  user interrupted bot talking
  if $mode == "interrupt"
    finish all bot actions
  elif $mode == "inform"
    start VisualInformationSceneAction(title="Please wait with talking!", support_prompts=["You should only talk after the avatar."], content=[])
    wait 3.0
  elif $mode == "ignore"
    log "Bot ignored user interruption"

# ----------------------------------
# LLM mechanics
# ----------------------------------

@loop("llm_response_polling")
flow polling llm request response $interval=1.0
  match StartGenerateUserIntentAction() as $event_ref
    or StartGenerateFlowContinuationAction() as $event_ref
    or StartGenerateFlowFromNameAction() as $event_ref
    or StartGenerateValueAction() as $event_ref
    or StartGenerateFlowFromInstructionsAction() as $event_ref
  start repeating timer "llm_response_polling" $interval as $polling_timer
  start bot posture "Thinking, idle" as $posture
  match $event_ref.action.Finished()
  send $polling_timer.Stop()
  send $posture.Stop()

flow generating user intent for unhandled user utterance
  """This is the fallback flow that takes care of unhandled user utterances and will generate a user intent."""
  activate tracking bot talking state
  global $bot_talking_state

  match UnhandledEvent(event="UtteranceUserActionFinished", loop_ids={$self.loop_id}) as $event
  if $bot_talking_state == False
    $transcript = $event.final_transcript
    log "generating user intent for unhandled user utterance: {$transcript}"
    $action = 'user said "{$transcript}"'
    $intent = await derive user intent from user action $action 20

  # We need to log the user action
  send UserActionLog(flow_id="user said", parameter=$event.final_transcript, intent_flow_id=$intent)
  # We need to add the generated user intent to the intent log
  send UserIntentLog(flow_id=$intent, parameter=None)

  # Generate the 'user intent' by sending out the FinishFlow event
  send FinishFlow(flow_id=$intent)

flow derive user intent from user action $user_action $max_example_flows -> $intent
  activate polling llm request response
  $intent = await GenerateUserIntentAction(user_action=$user_action, max_example_flows=$max_example_flows)
  return $intent

flow generate interaction continuation -> $flow_name
  activate polling llm request response
  # Generate continuation based current interaction history
  $flow_info = await GenerateFlowContinuationAction(temperature=0.1)

  $exists = await CheckValidFlowExistsAction(flow_id=$flow_info.name)
  if $exists == False
    $flows = await AddFlowsAction(config=$flow_info.body)
    if len($flows) == 0
      print "LLM generated flow parsing failed!"
      bot say "Sorry, what did you say?"
      return None

  $flow_name = $flow_info.name
  return $flow_info.name

flow generate then continue interaction
  $generated_flow_name = await generate interaction continuation
  if $generated_flow_name != None
    await await_flow_by_name $generated_flow_name
    await RemoveFlowsAction(flow_ids=[$generated_flow_name])

# ----------------------------------
# Experimental flows
# ----------------------------------

flow responding to unhandled user intent
  """This is the fallback flow that takes care of unhandled user utterances and will generate a user intent."""
  await unhandled user intent
  $flow_name = await generate interaction continuation
  await await_flow_by_name $flow_name
  await RemoveFlowsAction(flow_ids=[$flow_name])

flow handling start of undefined flow
  """We want to start an undefined flow."""

  match UnhandledEvent(event="StartFlow") as $event

  start_new_flow_instance:

  if search('^user ',$event.flow_id)

    # We have an undefined user intent, so we just fake it to be started by this fallback flow
    send FlowStarted(flow_id=$event.flow_id, flow_instance_uid=$event.flow_instance_uid)
    # Once this fallback flow receives the user intent it will finish and therefore also trigger the original matcher
    match FlowFinished(flow_id=$event.flow_id)

  else

    # We have an undefined bot intent, let's generate a new flow for it

    $flow_source = await GenerateFlowFromNameAction(name=$event.flow_id)

    await AddFlowsAction(config=$flow_source)
    $new_flow_instance_uid = "($event.flow_id){uid()}"
    send StartFlow(flow_id=$event.flow_id, flow_instance_uid=$new_flow_instance_uid)
    match FlowStarted(flow_id=$event.flow_id, flow_instance_uid=$new_flow_instance_uid) as $event_ref
    match $event_ref.flow.Finished()
    await RemoveFlowsAction(flow_ids=[$event.flow_id])

flow execute llm instruction $instructions
  """This will create a new flow based on the provided instructions and start it."""
  $flow_info = await GenerateFlowFromInstructionsAction(instructions=$instructions)

  await AddFlowsAction(config=$flow_info.body)

  $new_flow_instance_uid = "($flow_info.name){uid()}"
  send StartFlow(flow_id=$flow_info.name, flow_instance_uid=$new_flow_instance_uid)
  match FlowStarted(flow_id=$flow_info.name, flow_instance_uid=$new_flow_instance_uid) as $event_ref
  match $event_ref.flow.Finished()
  await RemoveFlowsAction(flow_ids=[$flow_info.name])

@meta(user_intent=True)
flow user requested a task
  user said "do something"
    or user said "can you do something"
    or user said "please do"

flow custom instructions
  user requested a task
  $instructions = await GetLastUserMessageAction()
  execute llm instruction $instructions


# --------------------------------------
# Posture Management Flows
# --------------------------------------
@loop("managing_listening_posture")
flow managing listening posture
  user started saying something
  start bot posture "listening"
  match UtteranceUserAction.Finished()

@loop("managing_talking_posture")
flow managing talking posture
  bot started saying something
  start bot posture "talking"
  bot said something

@loop("managing_thinking_posture")
flow managing thinking posture
  global $bot_talking_state

  match UtteranceUserAction.Finished()
  if $bot_talking_state
    log "Do not switch to thinking posture because bot is talking"
  else
    start bot posture "thinking"
    bot started saying something

@loop("managing_bot_postures")
flow managing bot postures
  activate track bot talking state
  activate managing listening posture
  activate managing thinking posture
  activate managing talking posture
  start bot posture "attentive"
  wait indefinitely

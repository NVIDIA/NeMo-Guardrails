flow autoalign check input
  $input_result = await AutoalignInputApiAction(show_autoalign_message=True)
  if $input_result["guardrails_triggered"]
    global $autoalign_input_response
    $autoalign_input_response = $input_result['combined_response']
    if $system.config.enable_rails_exceptions
      send AutoAlignInputRailException(message="AutoAlign input guardrail triggered")
    else
      bot refuse to respond
    abort
  else if $input_result["pii_fast"] and $input_result["pii_fast"]["guarded"]:
    global $user_message
    $user_message = $input_result["pii_fast"]["response"]

flow autoalign check output
  $output_result = await AutoalignOutputApiAction(show_autoalign_message=True)
  if $output_result["guardrails_triggered"]
    if $system.config.enable_rails_exceptions
      send AutoAlignOutputRailException(message="AutoAlign guardrail triggered")
    else
      bot refuse to respond
    abort
  else
    global $pii_message_output
    $pii_message_output = $output_result["pii_fast"]["response"]
    if $output_result["pii_fast"]["guarded"]
      global $bot_message
      $bot_message = $pii_message_output

flow autoalign factcheck output
  if $check_facts == True
    global $check_facts
    $check_facts = False
    global $threshold
    $threshold = 0.5
    $output_result = await AutoalignFactcheckOutputApiAction(factcheck_threshold=$threshold, show_autoalign_message=True)
    bot provide response

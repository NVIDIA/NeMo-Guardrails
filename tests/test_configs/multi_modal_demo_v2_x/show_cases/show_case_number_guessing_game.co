import core
import avatars
import llm
import timing

flow showcase number guessing game
  """Ask the user to guess a random number between 1 and 100; after each guess let the user know if the number was to low or high. If the user guesses the correct number, congratulate!"""
  activate polling llm request response
  activate reaction bot question repetition for user silence 10

  $random_number = ..."Random number between 1 and 100 as an explicit integer (e.g. '42')."
  if not is_int($random_number)
    $random_number = 42

  start scene show textual information $title="Little Game" $text="Guess the random number between 0 and 100 (hint: {$random_number})" $header_image="question mark horizontal orientation" as $scene

  bot ask "Guess the random number between 0 and 100!"
    and bot gesture "Waving hands"

  #activate reaction to user silence 20.0 "Just say a number between 0 and 100!" as $repeat_question

  $attempts = 1
  while True
    user said something
    $user_number = ..."The user mentioned number as an integer. 'None' if the user did not mention a number."
    log "extracted user number {$user_number}"
    if not is_int($user_number)
      bot express "I don't think this is a number!"
        and bot gesture "frown"
      bot ask "Just guess a number between 0 and 100!"
    elif $user_number < $random_number
      #bot inform that the guessed number is too low
      bot inform "{$user_number} is too low!"
        and bot gesture "shaking head"
    elif $user_number > $random_number
      #bot inform that the guessed number is too high
      bot inform "{$user_number} is too high!"
        and bot gesture "shaking head"
    else
      #stop $scene
      send $scene.Stop()
      #bot express congratulation and shows happiness
      bot express "Congratulation! {$user_number} is the correct number!"
        and bot gesture "smiling, dancing cheerful"
      break

    if $attempts == 3
      bot ask "You can do this! What could be the number?" and bot gesture "point towards the user"
    else
      bot ask "Try again!" or bot ask "Guess again!" or bot ask "Another guess?" or bot ask "Next try!"
    $attempts = $attempts + 1

  #bot inform about the number of attempts it took the user to find the number
  bot inform "It took you {$attempts} attempts to find the correct number!"
